@echo off
rem Force the script to use the Azul Zulu Java JRE
set "LOCAL_JAVA=C:\Program Files (x86)\Zulu\Zulu-8-jre\bin\javaw.exe"

rem Ensure Java exists before proceeding
if not exist "%LOCAL_JAVA%" (
    echo ERROR: Java not found at %LOCAL_JAVA%
    exit /b 1
)

echo Using Java: %LOCAL_JAVA%

set basedir=%~f0
:strip
set removed=%basedir:~-1%
set basedir=%basedir:~0,-1%
if NOT "%removed%"=="\" goto strip
set SQUIRREL_SQL_HOME=%basedir%

"%LOCAL_JAVA%" -cp "%SQUIRREL_SQL_HOME%\lib\versioncheck.jar" JavaVersionChecker 1.8 9 10 11 12 13 14 15 16
if ErrorLevel 1 goto ExitForWrongJavaVersion

:launchsquirrel
SET CP="%SQUIRREL_SQL_HOME%\squirrel-sql.jar;%SQUIRREL_SQL_HOME%\lib\*"
echo "CP=%CP%"

SET TMP_PARMS=--log-config-file "%SQUIRREL_SQL_HOME%\log4j.properties" --squirrel-home "%SQUIRREL_SQL_HOME%" %1 %2 %3 %4 %5 %6 %7 %8 %9

rem Start the SQuirreL SQL Client using Azul Zulu Java JRE
start "SQuirreL SQL Client" /B "%LOCAL_JAVA%" -cp %CP% -Dsquirrel.nopixfmt=true -Dsun.java2d.noddraw=true %SQUIRREL_SQL_OPTS% -splash:"%SQUIRREL_SQL_HOME%/icons/splash.jpg" net.sourceforge.squirrel_sql.client.Main %TMP_PARMS%

:ExitForWrongJavaVersion
